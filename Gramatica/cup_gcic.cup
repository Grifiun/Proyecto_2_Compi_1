package gramatica_gcic;

import java_cup.runtime.*;
import gramatica_gcic.sym;
import gramatica_gcic.LexerGCIC;
import java_cup.runtime.XMLElement;
import clasesDAO.Token;
import clasesDAO.TokenError;
import java.util.ArrayList;
import java.util.List;

parser code{:
    //Listado de errores	
	//Controlador de error sintactico
    ArrayList<TokenError> listadoErroresSintacticos = new ArrayList();
	
	@Override
	public void syntax_error(Symbol a){
		Token tokenError = (Token) a.value;//lo transformamos en token para obtener su lexema, fila y columna		
		String msgError = "Se esperaba: ";	

		
		int linea = tokenError.getLinea();
		int columna = tokenError.getColumna();

		for(int i = 0; i < expected_token_ids().size(); i++){
			//msgError = msgError + mensajes[expected_token_ids().get(i)];
			msgError = msgError + symbl_name_from_id(expected_token_ids().get(i)) + " ";

			if(i < expected_token_ids().size() - 1)
				msgError = msgError + ", ";			
		}
        		System.out.println("error de sintaxis: linea: "+tokenError.getLexema()+" "+tokenError.getLinea()+" columna: "+tokenError.getColumna()+" MENSAJE: "+msgError);

        TokenError tokenErrorAux = new TokenError("ERROR SINTACTICO", tokenError.getLexema(), msgError, linea, columna);
        listadoErroresSintacticos.add(tokenErrorAux);
	}
	
	public void report_error(String message, Object info) {
        System.out.println("Reportar Error");
    }
    
	public void report_fatal_error(String message, Object info) {
        System.out.println("Error Fatal: " + info);
    }

    protected int error_sync_size() {
        return 1;
    }

	public ArrayList<TokenError> getListadoErroresParser(){
		return listadoErroresSintacticos;
	}

    public parser(LexerGCIC lex){
        super(lex);
    }
:}

//SIMBOLOS
terminal Token PERCENT     ;
terminal Token IGUAL       ;
terminal Token MENOR       ;
terminal Token MAYOR       ;
terminal Token CORCH_INI   ;
terminal Token CORCH_FIN   ;
terminal Token PAREN_INI   ;
terminal Token PAREN_FIN   ;
terminal Token LLAVE_INI   ;
terminal Token LLAVE_FIN   ;
terminal Token COMA        ;
terminal Token PUNTO_COMA  ;
terminal Token DOS_PUNTOS  ;
terminal Token IGUAL_IGUAL ;
terminal Token NO_IGUAL    ;
terminal Token MENOR_IGUAL ;
terminal Token MAYOR_IGUAL ;

//OPERADORES LOGICOS
terminal Token SIMBOL_OR   ;
terminal Token SIMBOL_AND  ;
terminal Token SIMBOL_NOT  ;

//OPERADORES MATEMATICOS
terminal Token SIGNO_MAS   ;
terminal Token SIGNO_MIN   ;
terminal Token SIGNO_POR   ;
terminal Token SIGNO_DIV   ;

//ETIQUETAS HTML
terminal Token C_GCIC      ;
terminal Token C_HEAD      ;
terminal Token C_TITLE     ;
terminal Token C_LINK      ;
terminal Token C_BODY      ;
terminal Token C_SPAM      ;
terminal Token C_INPUT     ;
terminal Token C_TEXTAREA  ;
terminal Token C_SELECT    ;
terminal Token C_OPTION    ;
terminal Token C_DIV       ;
terminal Token C_IMG       ;
terminal Token C_BR        ;
terminal Token C_BUTTON    ;
terminal Token C_H1        ;
terminal Token C_P         ;
//SCRIPTING
terminal Token C_SCRIPTING ;

//PARAMETROS
terminal Token HREF        ;
terminal Token BACKGROUND  ;
terminal Token COLOR       ;
terminal Token FONT_SIZE   ;
terminal Token FONT_FAMILY ;
terminal Token TEXT_ALIGN  ;
terminal Token TYPE        ;
terminal Token ID          ;
terminal Token NAME        ;
terminal Token COLS        ;
terminal Token ROWS        ;
terminal Token CLASS       ;
terminal Token SRC         ;
terminal Token WIDTH       ;
terminal Token HEIGHT      ;
terminal Token ALT         ;
terminal Token ONCLICK     ;

//PARAMETROS SCRIPTING
terminal Token ON_LOAD     ;
terminal Token PROCESS_ABC ;

//TIPO DATOS
terminal Token INTEGER     ;
terminal Token DECIMAL     ;
terminal Token BOOLEAN     ;
terminal Token CHAR        ;
terminal Token STRING      ;

//FUNCIONES ESPECIALES LENGUAJE CLC
terminal Token FUNCTION_ASC                ;
terminal Token FUNCTION_DESC               ;
terminal Token FUNCTION_LETPAR_NUM         ;
terminal Token FUNCTION_LETIMPAR_NUM       ;
terminal Token FUNCTION_REVERSE            ;
terminal Token FUNCTION_CARACTER_ALEATORIO ;
terminal Token FUNCTION_NUM_ALEATORIO      ;
terminal Token FUNCTION_ALERT_INFO         ;
terminal Token FUNCTION_EXIT               ;

//ACCESO A IDENTIFICADORES
terminal Token GET_ELEMEN_BY_ID            ;

//BLOQUE INSTRUCCIONES
terminal Token INIT        ;
terminal Token END         ;

//SENTENCIAS
terminal Token IF          ;
terminal Token ELSE        ;
terminal Token ELSE_IF     ;
terminal Token THEN        ;

//CICLOS
terminal Token REPEAT      ;
terminal Token HUNTIL      ;
terminal Token WHILE       ;
terminal Token THENWHILE   ;

//INSERT
terminal Token INSERT      ;

//MODO DECLARACION
terminal Token MODO_GLOBAL ;

//COMENTARIOS, los ignoramos
terminal Token COMENT_BLOCK;
terminal Token COMENT_LINE ;

//LLAMADA A UN PROCESO
terminal Token PROCESS_VAL ;

////////////////////////////////////VALUES C_HTML
//COLOR VALUES
terminal Token COLOR_HEX   ;
terminal Token COLOR_VALUE ;

//PIXELS VALUE
terminal Token PIXEL_VALUE ;
terminal Token PERCE_VALUE ;

//FONT VALUE
terminal Token FONT_VALUE  ;

//TEXT ALIGN VALUE
terminal Token ALIGN_VALUE ;

//TYPE INPUT VALUE
terminal Token TEXT        ;
terminal Token NUMBER      ;
terminal Token RADIO       ;
terminal Token CHECKBOX    ;

//NUMBERS VALUE
terminal Token DIGIT_VALUE ;

//CLASS VALUE
terminal Token ROW         ;
terminal Token COLUMN      ;

//URL VALUE
//URL_VALUE   ; \" [\0-\40]* "https://www." [\41-\176]+    [\0-\40]* \" //excluimos el espacio

//ID VALUE, inicia con "_" | "-" | "$", alfabeto y sigue con alfanum o los mismos simbolos
terminal Token ID_VALUE_STR  ;
terminal Token ID_VALUE_CHAR ;

//TEXT VALUE, se excluye el codigo ascii 42, el cual representa a << " >>
terminal Token TEXT_VALUE          ;
terminal Token INSERT_TOKEN_VALUE  ;

//DATA VALUES
terminal Token INTEGER_VALUE   ;
terminal Token DECIMAL_VALUE   ;
terminal Token BOOLEAN_VALUE   ;
terminal Token CHARACT_VALUE   ;
//DECIMAL_VALUE ;

//////////////////////////////////IDS
terminal Token ID_VALUE        ;
terminal Token ALL_CHARACTERS  ;

////////////////////////////////////////////////////////////////NO TERMINALES
non terminal s;

//////////////////////////////////////////////////PRODUCCIONES ETIQUETAS
/////////////////////////CONTENIDO
non terminal etiqueta_gcic;
non terminal etiqueta_head;
non terminal etiqueta_title;
non terminal etiqueta_link;
non terminal etiqueta_body;
non terminal etiqueta_spam;
non terminal etiqueta_input;
non terminal etiqueta_textarea;
non terminal etiqueta_select;
non terminal etiqueta_option;
non terminal etiqueta_div;
non terminal etiqueta_img;
non terminal etiqueta_br;
non terminal etiqueta_button;
non terminal etiqueta_h1;
non terminal etiqueta_p;
non terminal etiqueta_scripting;

non terminal String contenido_texto;
non terminal String contenido_texto_unidad;
non terminal bloque_elements_gcic;
non terminal bloque_options;
non terminal bloque_elements_head;
non terminal elements_head;
non terminal bloque_elements_body;
non terminal elements_body;
/////////////////////////APERTURA
non terminal etiqueta_apertura_gcic;
non terminal etiqueta_apertura_head;
non terminal etiqueta_apertura_title;
non terminal etiqueta_apertura_link;
non terminal etiqueta_apertura_body;
non terminal etiqueta_apertura_spam;
non terminal etiqueta_apertura_input;
non terminal etiqueta_apertura_textarea;
non terminal etiqueta_apertura_select;
non terminal etiqueta_apertura_option;
non terminal etiqueta_apertura_div;
non terminal etiqueta_apertura_img;
non terminal etiqueta_apertura_br;
non terminal etiqueta_apertura_button;
non terminal etiqueta_apertura_h1;
non terminal etiqueta_apertura_p;
non terminal etiqueta_apertura_sripting;
/////////////////////////CIERRE
non terminal etiqueta_cierre_gcic;
non terminal etiqueta_cierre_head;
non terminal etiqueta_cierre_title;
non terminal etiqueta_cierre_link;
non terminal etiqueta_cierre_body;
non terminal etiqueta_cierre_spam;
non terminal etiqueta_cierre_input;
non terminal etiqueta_cierre_textarea;
non terminal etiqueta_cierre_select;
non terminal etiqueta_cierre_option;
non terminal etiqueta_cierre_div;
non terminal etiqueta_cierre_img;
non terminal etiqueta_cierre_button;
non terminal etiqueta_cierre_h1;
non terminal etiqueta_cierre_p;
non terminal etiqueta_cierre_sripting;

//////////////////////////////////////////////////BLOQUE PARAMETROS
non terminal bloque_parametros_gcic;
non terminal bloque_parametros_link;
non terminal bloque_parametros_body;
non terminal bloque_parametros_input;
non terminal bloque_parametros_textarea;
non terminal bloque_parametros_div;
non terminal bloque_parametros_img;
non terminal bloque_parametros_label;
non terminal bloque_parametros_button;

//////////////////////////////////////////////////PARAMETROS 
non terminal parametros_gcic;
non terminal parametros_link;
non terminal parametros_input;
non terminal parametros_textarea;
non terminal parametros_div;
non terminal parametros_background;
non terminal parametros_button;
non terminal parametros_letras; 
non terminal parametros_img;

//////////////////////////////////////////////////PARAMETRO UNIDAD
non terminal parametro_unidad_href;
non terminal parametro_unidad_background;
non terminal parametro_unidad_color;
non terminal parametro_unidad_font_size;
non terminal parametro_unidad_font_family;
non terminal parametro_unidad_text_align;
non terminal parametro_unidad_type;
non terminal parametro_unidad_id;
non terminal parametro_unidad_name;
non terminal parametro_unidad_cols;
non terminal parametro_unidad_rows;
non terminal parametro_unidad_class;
non terminal parametro_unidad_src;
non terminal parametro_unidad_width;
non terminal parametro_unidad_height;
non terminal parametro_unidad_alt;
non terminal parametro_unidad_onclick;

//non terminal parametro_unidad_option;
//////////////////////////////////////////////////VALORES
non terminal parametro_valor_color;
non terminal parametro_valor_size;
non terminal parametro_valor_type;
non terminal parametro_valor_id;
non terminal parametro_valor_name;
non terminal parametro_valor_class;
non terminal parametro_valor_reservado;

//////////////////////////////////////////////////SCRIPTING
//FUNCION
non terminal bloque_funciones;
non terminal funcion_unitaria;
non terminal nombre_funciones;
//BLOQUE INSTRUCCIONES
non terminal bloque_instrucciones_sentencias;
non terminal bloque_instrucciones_sentencias_inicio;
non terminal bloque_instrucciones_sentencias_fin;
non terminal bloque_instrucciones;
non terminal instruccion_general;
non terminal instruccion;
//DECLARACION Y ASIGNACION
non terminal intruccion_declaracion;
non terminal intruccion_asignacion;
non terminal asignacion;
non terminal valores_datos_primarios;
non terminal tipo_datos;
non terminal nombres_variables;
non terminal nombres_variables_unidad;
//MODO
non terminal modo_variable;
//IF, ELSE IF, ELSE
non terminal bloque_sentencia_if;
non terminal bloque_if;
non terminal bloque_if_elses;
non terminal comparacion_if;
non terminal comparacion_else_if;
non terminal comparacion_else;
//REPEAT AND WHILE
non terminal instruccion_ciclos;
non terminal ciclo_repeat;
non terminal ciclo_while;
non terminal ciclo_while_declaracion;
non terminal ciclo_repeat_declaracion;
non terminal condicion_repeat_inicio;
non terminal condicion_repeat_fin;
//INSERT
non terminal instruccion_insert;
non terminal valores_insert_token;
non terminal valores_insert_token_unidad;
//GET ELEMENT
non terminal get_element_value;
non terminal get_element_value_id;
//CONDICION Retorna true or false
non terminal condicion;
//OPERACIONES
non terminal operacion_logica;
non terminal operacion_relacional;
non terminal operacion_aritmetica;

////////////////////////////////////////////////////////////////PRECEDENCIA
precedence left ELSE;
precedence left ELSE_IF;
//precedencia aritmetica
precedence left SIGNO_MAS, SIGNO_MIN;
precedence left SIGNO_POR, SIGNO_DIV;
//precedencia relacional
precedence nonassoc MAYOR_IGUAL, MENOR_IGUAL, MENOR, MAYOR;
precedence nonassoc IGUAL_IGUAL, NO_IGUAL;
//precedencia logica
precedence right SIMBOL_NOT;
precedence left SIMBOL_AND;
precedence left SIMBOL_OR;

////////////////////////////////////////////////////////////////PRODUCCIONES
start with s;

s ::= etiqueta_gcic {: System.out.println("Se registro una etiqueta GCIC"); :}
	| /* empty */   {: System.out.println("Se registro una etiqueta vacia"); :}
	| etiqueta_scripting {: System.out.println("Se registro una etiqueta sripting"); :}
	;

//////////////////////////////////////////////////PRODUCCIONES ETIQUETAS
/////////////////////////CONTENIDO
etiqueta_gcic     ::= etiqueta_apertura_gcic bloque_elements_gcic etiqueta_cierre_gcic;
etiqueta_head     ::= etiqueta_apertura_head bloque_elements_head etiqueta_cierre_head;
etiqueta_body     ::= etiqueta_apertura_body bloque_elements_body etiqueta_cierre_body;
etiqueta_div      ::= etiqueta_apertura_div  bloque_elements_body etiqueta_cierre_div ;

bloque_elements_gcic ::= etiqueta_head etiqueta_body; //Head y el Body, obligatorio la declaracion de estos 2 terminos

bloque_elements_head ::= bloque_elements_head elements_head
	| /* empty */
	;

elements_head ::= 
	  etiqueta_title
	| etiqueta_link
	;

bloque_elements_body ::= bloque_elements_body elements_body
	| /* empty */
	;

elements_body ::= 
	  etiqueta_spam
	| etiqueta_input
	| etiqueta_textarea
	| etiqueta_select
	| etiqueta_div
	| etiqueta_img
	| etiqueta_br
	| etiqueta_button
	| etiqueta_h1
	| etiqueta_p
	| etiqueta_scripting 
	;

/////////////////////////SELECT
etiqueta_select   ::= etiqueta_apertura_select 
					  	bloque_options
					  etiqueta_cierre_select;

bloque_options    ::= bloque_options etiqueta_option
	| /* empty */
	;

/////////////////////////NADA
etiqueta_br       ::= etiqueta_apertura_br;
etiqueta_img      ::= etiqueta_apertura_img etiqueta_cierre_img;

/////////////////////////TEXTOS
etiqueta_title    ::= etiqueta_apertura_title    contenido_texto etiqueta_cierre_title   ;
etiqueta_link     ::= etiqueta_apertura_link     contenido_texto etiqueta_cierre_link    ;
etiqueta_spam     ::= etiqueta_apertura_spam     contenido_texto etiqueta_cierre_spam    ;
etiqueta_input    ::= etiqueta_apertura_input    contenido_texto etiqueta_cierre_input   ;
etiqueta_textarea ::= etiqueta_apertura_textarea contenido_texto etiqueta_cierre_textarea;
etiqueta_option   ::= etiqueta_apertura_option   contenido_texto etiqueta_cierre_option  ;
etiqueta_button   ::= etiqueta_apertura_button   contenido_texto etiqueta_cierre_button  ;
etiqueta_h1       ::= etiqueta_apertura_h1       contenido_texto etiqueta_cierre_h1      ;
etiqueta_p        ::= etiqueta_apertura_p        contenido_texto etiqueta_cierre_p       ;


contenido_texto   ::= contenido_texto contenido_texto_unidad
		{:
			//RESULT = texto_aux + "\n";//agregamos salto de linea
		:}
	| contenido_texto:texto_aux etiqueta_br //salto de linea
		{:
			RESULT = texto_aux + "\n";//agregamos salto de linea
		:}
	| /* empty */ 
		{:
			RESULT = "";
		:}
	;

contenido_texto_unidad ::= ALL_CHARACTERS
	| ID_VALUE
	//SIMBOLOS
	| PERCENT
	| IGUAL
	| CORCH_INI
	| CORCH_FIN
	| PAREN_INI
	| PAREN_FIN
	| LLAVE_INI
	| LLAVE_FIN
	| COMA
	| PUNTO_COMA
	| DOS_PUNTOS
	| NO_IGUAL
	| MAYOR_IGUAL
	| MENOR_IGUAL
	//OPERADORES LOGICOS
	| SIMBOL_OR
	| SIMBOL_AND
	| SIMBOL_NOT
	//OPERADORES MATEMATICOS
	| SIGNO_MAS
	| SIGNO_MIN
	| SIGNO_POR 
	| SIGNO_DIV	
	//ETIQUETAS HTML
	| C_GCIC
	| C_HEAD
	| C_TITLE
	| C_LINK
	| C_BODY
	| C_SPAM
	| C_INPUT
	| C_TEXTAREA
	| C_SELECT
	| C_OPTION
	| C_DIV
	| C_IMG
	| C_BR
	| C_BUTTON
	| C_H1
	| C_P
	//SCRIPTING
	| C_SCRIPTING 
	//PARAMETROS
	| HREF
	| BACKGROUND
	| COLOR
	| FONT_SIZE
	| FONT_FAMILY
	| TEXT_ALIGN
	| TYPE
	| ID
	| NAME
	| COLS
	| ROWS
	| CLASS
	| SRC
	| WIDTH
	| HEIGHT
	| ALT
	| ONCLICK
	//PARAMETROS SCRIPTING
	| ON_LOAD
	| PROCESS_ABC
	//TIPO DATOS
	| INTEGER
	| DECIMAL
	| BOOLEAN
	| CHAR
	| STRING
	//FUNCIONES ESPECIALES LENGUAJE CLC
	| FUNCTION_ASC
	| FUNCTION_DESC
	| FUNCTION_LETPAR_NUM
	| FUNCTION_LETIMPAR_NUM
	| FUNCTION_REVERSE
	| FUNCTION_CARACTER_ALEATORIO
	| FUNCTION_NUM_ALEATORIO
	| FUNCTION_ALERT_INFO
	| FUNCTION_EXIT
	//ACCESO A IDENTIFICADORES
	| GET_ELEMEN_BY_ID
	//BLOQUE INSTRUCCIONES
	| INIT
	| END
	//SENTENCIAS
	| IF
	| ELSE
	| THEN
	//CICLOS
	| REPEAT
	| HUNTIL 
	| WHILE
	| THENWHILE
	//INSERT
	| INSERT
	//MODO DECLARACION
	| MODO_GLOBAL
	//DATA VALUES
	| INTEGER_VALUE
	| DECIMAL_VALUE
	| BOOLEAN_VALUE
	//VALORES CON COMILLAS
	| TEXT_VALUE
	| ID_VALUE_STR
	| DIGIT_VALUE
	;

/////////////////////////SCRIPTING
etiqueta_scripting ::= etiqueta_apertura_sripting bloque_funciones etiqueta_cierre_sripting;
 /////////////////////////APERTURA, DE LA FORMA <C_NOMBRE_ETIQUETA [parametro1 = “valor”] [parametro2 = “valor”] [otro= “valor”] >
etiqueta_apertura_gcic     ::= MENOR C_GCIC     bloque_parametros_gcic     MAYOR;
etiqueta_apertura_head     ::= MENOR C_HEAD     /* NO PARAMETROS */        MAYOR; 
etiqueta_apertura_title    ::= MENOR C_TITLE    /* NO PARAMETROS */        MAYOR;
etiqueta_apertura_link     ::= MENOR C_LINK     bloque_parametros_link     MAYOR;
etiqueta_apertura_body     ::= MENOR C_BODY     bloque_parametros_body     MAYOR;
etiqueta_apertura_spam     ::= MENOR C_SPAM     bloque_parametros_label    MAYOR;
etiqueta_apertura_input    ::= MENOR C_INPUT    bloque_parametros_input    MAYOR;
etiqueta_apertura_textarea ::= MENOR C_TEXTAREA bloque_parametros_textarea MAYOR;
etiqueta_apertura_select   ::= MENOR C_SELECT   bloque_parametros_label    MAYOR;
etiqueta_apertura_option   ::= MENOR C_OPTION   /* NO PARAMETROS */        MAYOR;
etiqueta_apertura_div      ::= MENOR C_DIV      bloque_parametros_div      MAYOR;
etiqueta_apertura_img      ::= MENOR C_IMG      bloque_parametros_img      MAYOR;
etiqueta_apertura_br       ::= MENOR C_BR       /* NO PARAMETROS */        MAYOR;
etiqueta_apertura_button   ::= MENOR C_BUTTON   bloque_parametros_button   MAYOR;
etiqueta_apertura_h1       ::= MENOR C_H1       bloque_parametros_label    MAYOR;
etiqueta_apertura_p        ::= MENOR C_P        bloque_parametros_label    MAYOR;
etiqueta_apertura_sripting ::= MENOR C_SCRIPTING /* NO PARAMETROS */       MAYOR;

/////////////////////////CIERRE, DE LA FORMA </C_NOMBRE_ETIQUETA>
etiqueta_cierre_gcic     ::= MENOR SIGNO_DIV C_GCIC      MAYOR;
etiqueta_cierre_head     ::= MENOR SIGNO_DIV C_HEAD      MAYOR;
etiqueta_cierre_title    ::= MENOR SIGNO_DIV C_TITLE     MAYOR;
etiqueta_cierre_link     ::= MENOR SIGNO_DIV C_LINK      MAYOR;
etiqueta_cierre_body     ::= MENOR SIGNO_DIV C_BODY      MAYOR;
etiqueta_cierre_spam     ::= MENOR SIGNO_DIV C_SPAM      MAYOR;
etiqueta_cierre_input    ::= MENOR SIGNO_DIV C_INPUT     MAYOR;
etiqueta_cierre_textarea ::= MENOR SIGNO_DIV C_TEXTAREA  MAYOR;
etiqueta_cierre_select   ::= MENOR SIGNO_DIV C_SELECT    MAYOR;
etiqueta_cierre_option   ::= MENOR SIGNO_DIV C_OPTION    MAYOR;
etiqueta_cierre_div      ::= MENOR SIGNO_DIV C_DIV       MAYOR;
etiqueta_cierre_img      ::= MENOR SIGNO_DIV C_IMG       MAYOR;
etiqueta_cierre_button   ::= MENOR SIGNO_DIV C_BUTTON    MAYOR;
etiqueta_cierre_h1       ::= MENOR SIGNO_DIV C_H1        MAYOR;
etiqueta_cierre_p        ::= MENOR SIGNO_DIV C_P         MAYOR;
etiqueta_cierre_sripting ::= MENOR SIGNO_DIV C_SCRIPTING MAYOR;

//////////////////////////////////////////////////BLOQUE PARAMETROS
bloque_parametros_gcic ::= bloque_parametros_gcic parametros_gcic
	| /* empty */
	;

bloque_parametros_link ::= bloque_parametros_link parametros_link
	| /* empty */
	;

bloque_parametros_body ::= bloque_parametros_body parametros_background
	| /* empty */
	;

bloque_parametros_input ::= bloque_parametros_input parametros_input
	| /* empty */
	;

bloque_parametros_textarea ::= bloque_parametros_textarea parametros_textarea
	| /* empty */
	;	

bloque_parametros_div ::= bloque_parametros_div parametros_div
	| /* empty */
	;

bloque_parametros_img ::= bloque_parametros_img parametros_img
	| /* empty */
	;

bloque_parametros_label ::= bloque_parametros_label parametros_letras
	| /* empty */
	;	
	
bloque_parametros_button ::= bloque_parametros_button parametros_button
	| /* empty */
	;


//////////////////////////////////////////////////PARAMETROS 	
parametros_gcic ::= parametro_unidad_id
	| parametro_unidad_name
	;
	
parametros_link ::= parametro_unidad_href
	;
	
parametros_input ::= parametro_unidad_type
	| parametros_letras
	;
	
parametros_textarea ::= parametro_unidad_cols
	| parametro_unidad_rows
	| parametros_letras
	;
	
parametros_div ::= parametro_unidad_class
	| parametros_letras
	| parametros_background
	;
		
parametros_img ::= parametro_unidad_src
	| parametro_unidad_width
	| parametro_unidad_height
	| parametro_unidad_alt
	| parametro_unidad_id
	;
	
parametros_letras ::= parametro_unidad_font_size
	| parametro_unidad_font_family
	| parametro_unidad_text_align
	| parametro_unidad_id
	| parametro_unidad_color
	;
	
parametros_button ::= parametro_unidad_onclick
	| parametros_letras
	| parametros_background
	;

parametros_background ::= parametro_unidad_background
	;
	
//////////////////////////////////////////////////PARAMETRO UNIDAD
parametro_unidad_href ::= CORCH_INI 
                            HREF IGUAL TEXT_VALUE
                          CORCH_FIN
	;

parametro_unidad_background ::= CORCH_INI
                                    BACKGROUND IGUAL parametro_valor_color
                                CORCH_FIN
	;

parametro_unidad_color ::=  CORCH_INI
                                COLOR IGUAL parametro_valor_color
                            CORCH_FIN                        
	;
    
parametro_unidad_font_size ::=  CORCH_INI
                                    FONT_SIZE IGUAL parametro_valor_size
                                CORCH_FIN
	;

parametro_unidad_font_family ::= CORCH_INI
                                    FONT_FAMILY IGUAL FONT_VALUE
                                CORCH_FIN
	;

parametro_unidad_text_align ::= CORCH_INI
                                    TEXT_ALIGN IGUAL ALIGN_VALUE
                                CORCH_FIN
	;

parametro_unidad_type ::= CORCH_INI
                            TYPE IGUAL parametro_valor_type
                          CORCH_FIN
	;

parametro_unidad_id ::= CORCH_INI
                            ID IGUAL parametro_valor_id
                        CORCH_FIN
	;

parametro_unidad_name ::= CORCH_INI
                            NAME IGUAL parametro_valor_name
                          CORCH_FIN
	;

parametro_unidad_cols ::= CORCH_INI
                            COLS IGUAL DIGIT_VALUE
                          CORCH_FIN
	;

parametro_unidad_rows ::= CORCH_INI
                            ROWS IGUAL DIGIT_VALUE
                          CORCH_FIN
	;

parametro_unidad_class ::= CORCH_INI
                            CLASS IGUAL parametro_valor_class
                          CORCH_FIN
	;

parametro_unidad_src ::=  CORCH_INI 
                            SRC IGUAL TEXT_VALUE
                          CORCH_FIN
	;

parametro_unidad_width ::= CORCH_INI 
                            WIDTH IGUAL parametro_valor_size
                           CORCH_FIN
	;

parametro_unidad_height ::= CORCH_INI 
                                HEIGHT IGUAL parametro_valor_size
                            CORCH_FIN
	;

parametro_unidad_alt ::= CORCH_INI 
                            ALT IGUAL parametro_valor_name
                         CORCH_FIN
	;

parametro_unidad_onclick ::= CORCH_INI 
                                ONCLICK IGUAL PROCESS_VAL
                             CORCH_FIN
	;

//////////////////////////////////////////////////VALORES
parametro_valor_color ::= COLOR_HEX
    | COLOR_VALUE
    ;

parametro_valor_size ::= PIXEL_VALUE
    | PERCE_VALUE
    ;

parametro_valor_type ::= TEXT
    | NUMBER
    | RADIO
    | CHECKBOX
    ;

parametro_valor_id ::= COLOR_VALUE
    | PIXEL_VALUE
    | PERCE_VALUE
    | FONT_VALUE
    | ALIGN_VALUE
    | TEXT
    | NUMBER
    | RADIO
    | CHECKBOX
    | DIGIT_VALUE
    | ROW
    | COLUMN
    | ID_VALUE_STR
    ;

parametro_valor_name ::= TEXT_VALUE
    /* pabras reservadas */
    | parametro_valor_reservado
    ;

parametro_valor_class ::= ROW
    | COLUMN
    ;

parametro_valor_reservado ::= COLOR_HEX
    | PROCESS_VAL
    | parametro_valor_id
    ;

//////////////////////////////////////////////////SCRIPTING
//FUNCION
bloque_funciones ::= bloque_funciones funcion_unitaria
		{: System.out.println ("Se ejecuto BLOQUE DE FUNCIONES  "); :}
	| /* empty */
	;
funcion_unitaria ::= nombre_funciones PAREN_INI PAREN_FIN CORCH_INI //PROCESS_hola()[
						bloque_instrucciones
					CORCH_FIN // ]
					{: System.out.println ("Se ejecuto FUNCION  "); :}
	;
nombre_funciones ::= PROCESS_ABC
	| ON_LOAD
	;

//BLOQUE INSTRUCCIONES
bloque_instrucciones_sentencias ::= bloque_instrucciones_sentencias_inicio //INIT {::
										bloque_instrucciones 					//BLOQUE DE INSTRUCCIONES
									bloque_instrucciones_sentencias_fin    //::} END
									{: System.out.println ("Se ejecuto BLOQUE DE SENTENCIAS INIT END  "); :}
	| instruccion_general //INSTRUCCION SOLITARIA
	;
bloque_instrucciones_sentencias_inicio ::= INIT LLAVE_INI DOS_PUNTOS
	;
bloque_instrucciones_sentencias_fin ::= DOS_PUNTOS LLAVE_FIN END
	;
bloque_instrucciones ::= bloque_instrucciones instruccion_general
	| /* empty */ 
	;
instruccion_general ::= instruccion PUNTO_COMA //INSTRUCCION QUE TERMINA EN PUNTO Y COMA
	| bloque_sentencia_if //IF, ELSE IF, ELSE
	| instruccion_ciclos //REPEAT Y WHILE
	;
instruccion ::= intruccion_declaracion {: System.out.println ("Se ejecuto declaracion  "); :}
	| intruccion_asignacion {: System.out.println ("Se ejecuto asignacion  "); :}
	| instruccion_insert {: System.out.println ("Se ejecuto insert  "); :}
	;
//DECLARACION Y ASIGNACION
intruccion_declaracion ::= tipo_datos modo_variable nombres_variables //declaracion del tipo << string @global a, b, c >>
	;
intruccion_asignacion ::= nombres_variables IGUAL asignacion //pueden asignarse    << a, b, c = "hola"; >>
	| intruccion_declaracion IGUAL asignacion //pueden asignarse con declaraciones << string a, b, c = "hola"; >>
	;
asignacion ::= 
	  //VALORES
      operacion_aritmetica //operacion aritmetica, entre variables, o valores primitivos
	;
	
valores_datos_primarios ::= INTEGER_VALUE
	| DECIMAL_VALUE 
	| BOOLEAN_VALUE
	| CHARACT_VALUE 
	| parametro_valor_name // Cualquier cadena de carateres encerradas entre << " " >>
	| get_element_value //para strings solamente
	| nombres_variables_unidad //variable << string a = b; >> donde b es un string
	;

tipo_datos ::= STRING
	| INTEGER
	| DECIMAL
	| CHAR
	| BOOLEAN
	;	
nombres_variables ::= nombres_variables COMA nombres_variables_unidad
	| nombres_variables_unidad
	;
nombres_variables_unidad ::= ID_VALUE
	//ETIQUETAS HTML
	| C_GCIC
	| C_HEAD
	| C_TITLE
	| C_LINK
	| C_BODY
	| C_SPAM
	| C_INPUT
	| C_TEXTAREA
	| C_SELECT
	| C_OPTION
	| C_DIV
	| C_IMG
	| C_BR
	| C_BUTTON
	| C_H1
	| C_P
	//SCRIPTING
	| C_SCRIPTING
	//PARAMETROS
	| HREF
	| BACKGROUND
	| COLOR
	| FONT_SIZE
	| FONT_FAMILY
	| TEXT_ALIGN
	| TYPE
	| ID
	| NAME
	| COLS
	| ROWS
	| CLASS
	| SRC
	| WIDTH
	| HEIGHT
	| ALT
	| ONCLICK
	;

//MODO
modo_variable ::= MODO_GLOBAL //@global
	| /* empty */
	;
//IF, ELSE IF, ELSE
bloque_sentencia_if ::= bloque_if //comparacion_else //bloque con else
		{: System.out.println ("Se ejecuto BLOQUE IF CON ELSE/sin  "); :}
		/*
			IF
				...
			ELSE IF
				...
			ELSE IF
				...
			ELSE
		*/
	/*| bloque_if  //bloque sin else
		{: System.out.println ("Se ejecuto BLOQUE IF SIN ELSE  "); :}
		/*
			IF
				...
			ELSE IF
				...
			ELSE IF
				...
		*/
	;
bloque_if ::= comparacion_if bloque_if_elses comparacion_else//bloque sin else
		{: System.out.println ("Se ejecuto BLOQUE IF ELSES  "); :}
	;
bloque_if_elses ::= bloque_if_elses comparacion_else_if //bloques de else if
	| /* empty */
	;
comparacion_if ::= IF PAREN_INI condicion PAREN_FIN THEN bloque_instrucciones_sentencias  //IF (condicion) .... 
		{: System.out.println ("Se ejecuto IF  "); :}
	;
comparacion_else_if ::= ELSE_IF PAREN_INI condicion PAREN_FIN THEN bloque_instrucciones_sentencias //comparacion_if  //ELSE IF ......
		{: System.out.println ("Se ejecuto ELSE IF  "); :}
	;
comparacion_else ::= ELSE bloque_instrucciones_sentencias // ELSE ...
		{: System.out.println ("Se ejecuto ELSE  "); :}
	| /* empty */
	;

//REPEAT AND WHILE
instruccion_ciclos ::= ciclo_repeat
	| ciclo_while
	;
ciclo_repeat ::= ciclo_repeat_declaracion bloque_instrucciones_sentencias {: System.out.println ("Se ejecuto ciclo repeat  "); :}
	;
ciclo_while ::= ciclo_while_declaracion bloque_instrucciones_sentencias {: System.out.println ("Se ejecuto ciclo while  "); :}
	;

ciclo_while_declaracion ::= WHILE PAREN_INI condicion PAREN_FIN THENWHILE
	;
ciclo_repeat_declaracion ::= REPEAT PAREN_INI condicion_repeat_inicio PAREN_FIN HUNTIL PAREN_INI condicion_repeat_fin PAREN_FIN
	;
condicion_repeat_inicio ::= intruccion_asignacion {: System.out.println ("Se ejecuto condicion inicio repeat  "); :}
	;
condicion_repeat_fin ::= //INTEGER_VALUE
	//| nombres_variables_unidad
	/*|*/ operacion_aritmetica
	;
//INSERT
instruccion_insert ::= INSERT PAREN_INI valores_insert_token PAREN_FIN {: System.out.println ("Se INSERT  "); :}
	;

valores_insert_token ::= valores_insert_token COMA valores_insert_token_unidad
	//| /* empty, generamos un '' */
	| valores_insert_token_unidad
	;

valores_insert_token_unidad ::= INSERT_TOKEN_VALUE //cualquier otra cadena encerrada entre ''
	| ID_VALUE_CHAR	//id encerrado entre ''
	| CHARACT_VALUE	//caracter
	| nombres_variables_unidad //variables aceptadas
	;

//GET ELEMENT
get_element_value ::= GET_ELEMEN_BY_ID PAREN_INI get_element_value_id PAREN_FIN {: System.out.println ("Se ejecuto get element byId  "); :}
	;
get_element_value_id ::= ID_VALUE_CHAR
	| CHARACT_VALUE
	;

//CONDICION Retorna true or false
condicion ::= operacion_logica;

//OPERACIONES
operacion_logica ::= operacion_logica SIMBOL_AND operacion_logica {: System.out.println ("Se ejecuto AND"); 	:}
    | operacion_logica SIMBOL_OR operacion_logica {: System.out.println ("Se ejecuto OR"); :}
    | SIMBOL_NOT operacion_logica {: System.out.println ("Se ejecuto NOT");	:}
	| operacion_relacional {: System.out.println ("Valor"); :}
    | PAREN_INI operacion_logica:p 
    {: 
		System.out.println(" ( )"); 	
	:}
	PAREN_FIN
    ;
//El valor que envian las operaciones racionales deben ser verdadero/falso
operacion_relacional::= 
	  operacion_aritmetica MAYOR       operacion_aritmetica {: System.out.println ("Se ejecuto >  "); :}
    | operacion_aritmetica MENOR       operacion_aritmetica {: System.out.println ("Se ejecuto <  "); :}
    | operacion_aritmetica MAYOR_IGUAL operacion_aritmetica {: System.out.println ("Se ejecuto >= "); :}
    | operacion_aritmetica MENOR_IGUAL operacion_aritmetica {: System.out.println ("Se ejecuto <= "); :}
    | operacion_aritmetica NO_IGUAL    operacion_aritmetica {: System.out.println ("Se ejecuto != "); :} 
    | operacion_aritmetica IGUAL_IGUAL operacion_aritmetica {: System.out.println ("Se ejecuto =  "); :} 
	| operacion_aritmetica                                  {: System.out.println ("Valor/variable"); :} 
    | error                                                 {: System.out.println ("Error op. rel."); :}
    ;
operacion_aritmetica ::= operacion_aritmetica:n1 SIGNO_MAS operacion_aritmetica:n2 
	{: 
		System.out.println(" + "); 	
	:}	
    | operacion_aritmetica:n1 SIGNO_MIN operacion_aritmetica:n2
    {: 
		System.out.println(" - "); 	
	:}
    | operacion_aritmetica:n1 SIGNO_POR operacion_aritmetica:n2 
    {: 
		System.out.println(" * "); 	
	:}
    | operacion_aritmetica:n1 SIGNO_DIV operacion_aritmetica:n2
    {: 
		System.out.println(" / "); 	
	:}
    | valores_datos_primarios:e
    {: 
		System.out.println ("Valor/variable");	
	:}
    | SIGNO_MIN operacion_aritmetica:n1
    {: 
		System.out.println(" Entero negativo "); :}	
    ;    